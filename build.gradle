plugins {
  id 'java'
  id 'groovy'
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id "com.katalon.gradle-plugin" version "0.0.7"
}

repositories {
  mavenCentral()
  maven {
    url 'https://jaspersoft.jfrog.io/jaspersoft/jaspersoft-repo/'
  }
  flatDir {
    dirs 'lib'
  }
}

def pluginSources = [
  'Keywords',
  'Test Listeners',
  'Include/scripts/groovy'
]

sourceSets {
  main {
    groovy {
      srcDirs = pluginSources
      srcDir 'Libs' // generated by Katalon Studio
    }
  }
}

shadowJar {
  exclude 'Temp*.class'
  from('Include/resources') {
    include '*.*'
  }
  from('Include/scripts/groovy/com/kms/katalon/core/reporting/basic/reporting/template'){
	  include '*.txt'
	  into 'com/kms/katalon/core/reporting/basic/reporting/template'
  }
}

groovydoc {
  source = pluginSources
  docTitle = 'Basic Report'
}

dependencies {
  compile 'commons-javaflow:commons-javaflow:20060411'
  compile('net.sf.jasperreports:jasperreports:6.2.0') {
    exclude group: 'org.springframework'
    exclude group: 'bouncycastle'
    exclude group: 'org.codehaus.castor'
    exclude group: 'com.google.zxing'
    exclude group: 'com.fasterxml.jackson.core'
    exclude group: 'org.apache.lucene'
    exclude group: 'org.jfree'
    exclude group: 'org.olap4j'
    exclude group: 'xml-apis'
  }
  compile 'net.sf.jasperreports:jasperreports-fonts:6.0.0'
}
