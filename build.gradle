plugins {
  id 'java'
  id 'groovy'
  id 'com.github.johnrengelman.shadow' version '6.1.0'
  id "com.katalon.gradle-plugin" version "0.1.2"
  id("org.sonarqube") version "4.0.0.2929"
}

sonar {
  properties {
    property("sonar.projectKey", "katalon-studio-basic-report-plugin")
    property("sonar.organization", "katalon-studio")
    property("sonar.host.url", "https://sonarcloud.io")
    property("sonar.sources", "Include\\scripts\\groovy")
  }
}

repositories {
  mavenCentral()
  maven {
    url 'https://jaspersoft.jfrog.io/jaspersoft/jaspersoft-repo/'
  }
  flatDir {
    dirs 'lib'
  }
}

def pluginSources = [
  'Keywords',
  'Test Listeners',
  'Include/scripts/groovy'
]

sourceSets {
  main {
    groovy {
      srcDirs = pluginSources
      srcDir 'Libs' // generated by Katalon Studio
    }
  }
}

shadowJar {
  exclude 'Temp*.class'
  from('Include/resources') {
    include '*.*'
  }
  from('Include/scripts/groovy/com/kms/katalon/core/reporting/basic/reporting/template'){
	  include '*.txt'
	  into 'com/kms/katalon/core/reporting/basic/reporting/template'
  }
}

groovydoc {
  source = pluginSources
  docTitle = 'Basic Report'
}

dependencies {
  implementation 'commons-javaflow:commons-javaflow:20060411'
  implementation('net.sf.jasperreports:jasperreports:6.20.5') {
    exclude group: 'org.springframework'
    exclude group: 'bouncycastle'
    exclude group: 'org.codehaus.castor'
    exclude group: 'com.google.zxing'
    exclude group: 'com.fasterxml.jackson.core'
    exclude group: 'org.apache.lucene'
    exclude group: 'org.jfree'
    exclude group: 'org.olap4j'
    exclude group: 'xml-apis'
  }
  implementation 'net.sf.jasperreports:jasperreports-fonts:6.20.5'
}
