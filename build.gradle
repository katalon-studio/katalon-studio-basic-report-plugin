plugins {
  id 'java'
  id 'groovy'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id "com.katalon.gradle-plugin" version "0.0.7"
  id("org.sonarqube") version "4.0.0.2929"
}

sonar {
  properties {
    property("sonar.projectKey", "katalon-studio-basic-report-plugin")
    property("sonar.organization", "katalon-studio")
    property("sonar.sources", 'Include/scripts/groovy')
  }
}

repositories {
  mavenCentral()
  maven {
    url 'https://jaspersoft.jfrog.io/jaspersoft/jaspersoft-repo/'
  }
  flatDir {
    dirs 'lib'
  }
}

def pluginSources = [
  'Keywords',
  'Test Listeners',
  'Include/scripts/groovy'
]

sourceSets {
  main {
    groovy {
      srcDirs = pluginSources
      srcDir 'Libs' // generated by Katalon Studio
    }
  }
}

shadowJar {
  exclude 'Temp*.class'
  from('Include/resources') {
    include '*.*'
  }
  from('Include/scripts/groovy/com/kms/katalon/core/reporting/basic/reporting/template'){
	  include '*.txt'
	  into 'com/kms/katalon/core/reporting/basic/reporting/template'
  }
}

groovydoc {
  source = pluginSources
  docTitle = 'Basic Report'
}
